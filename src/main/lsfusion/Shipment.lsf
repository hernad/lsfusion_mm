// # header
// # module
MODULE Shipment;
// # module end

// # require
REQUIRE Stock, Item, LegalEntity;
// # require end
// # header end

// # class
CLASS Shipment 'Consignment note';
CLASS ShipmentDetail 'Shipment line';

shipment 'Line Document' = DATA Shipment (ShipmentDetail) NONULL DELETE;
index 'Line number' (ShipmentDetail d) =
        PARTITION SUM 1 IF d IS ShipmentDetail
        ORDER d BY shipment (d);
// # class end

// # headerattributes
number 'Invoice number' = DATA BPSTRING[10] (Shipment);
date 'Invoice Date' = DATA DATE (Shipment);

customer 'Customer' = DATA LegalEntity (Shipment);
nameCustomer 'Name of the customer' (Shipment s) = name (customer (s));

stock 'Warehouse' = DATA Stock (Shipment);
nameStock 'Warehouse name' (Shipment s) = name (stock(s));
// # headerattributes end

// # detailattributes
item 'Product' = DATA Item (ShipmentDetail);
nameItem 'Item name' (ShipmentDetail d) = name (item (d));

quantity 'Quantity' = DATA NUMERIC[16,4] (ShipmentDetail);
price 'Sale price' = DATA NUMERIC[17,2] (ShipmentDetail);
sum 'Amount of sale' (ShipmentDetail d) = quantity (d) * price (d);
// # detailattributes end

// # priceset
price (ShipmentDetail d) <- salePrice (item (d)) WHEN CHANGED (item (d));
// # priceset end

// # formedit
FORM shipment 'Invoice'
OBJECTS s = Shipment PANEL
PROPERTIES (s) number, date, nameCustomer, nameStock

OBJECTS d = ShipmentDetail
PROPERTIES (d) nameItem, quantity, price, sum READONLY, NEW, DELETE GRID
FILTERS shipment (d) = s

EDIT Shipment OBJECT s
;
// # formedit end

// # formlist
FORM shipments 'Consignment notes'
OBJECTS s = Shipment
PROPERTIES (s) READONLY number, date, nameCustomer, nameStock
PROPERTIES (s) NEWSESSION NEW, EDIT, DELETE

OBJECTS d = ShipmentDetail
PROPERTIES (d) READONLY nameItem, quantity, price, sum
FILTERS shipment (d) = s
;
// # formlist end
